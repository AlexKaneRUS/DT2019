\import Utils
\import hw02 (fac)
\import lect03

-- 1. Докажите ассоциативность && и ||.

\func &&-assoc (x y z : Bool) : (x && y) && z = x && (y && z) \elim x
  | false => idp
  | true => idp

\func ||-assoc (x y z : Bool) : (x || y) || z = x || (y || z) \elim x
  | false => idp
  | true => idp

-- 2. Докажите, что fac 3 равно 6.

\func fac3-equals-6 : fac 3 = 6 => idp

-- 3. Докажите следующие пропозициональные тавтологии.

-- t1 : (P and Q -> R) -> P -> Q -> R
\func t1 {P Q R : \Type} (f : \Sigma P Q -> R) (p : P) (q : Q) : R => f (p, q)

-- t2 : (P -> R) -> (Q -> R) -> P or Q -> R
\func t2 {P Q R : \Type} (f : P -> R) (g : Q -> R) (h : Either P Q) : R \elim h
  | inl p => f p
  | inr q => g q

-- t3 : ((P -> Q -> R) -> P) -> (P -> R) -> R
\func t3 {P Q R : \Type} (f : (P -> Q -> R) -> P) (g : P -> R) : R => g (f (\lam p q => g p))

-- 4. Сформулируйте и докажите следующие пропозициональные тавтологии.

-- t4 : (P -> Q -> R) -> P and Q -> R

\func t4 {P Q R : \Type} (f : P -> Q -> R) (p : \Sigma P Q) : R \elim p
  | (x, y) => f x y

-- t5 : (P or Q -> P and Q) -> (P -> Q) and (Q -> P)

\func t5 {P Q : \Type} (f : Either P Q -> \Sigma P Q) : \Sigma (P -> Q) (Q -> P) => (lemma1 f, lemma2 f)
  \where {
    \func lemma1 {P Q : \Type} (f : Either P Q -> \Sigma P Q) (x : P) : Q => (f (inl x)).2
    \func lemma2 {P Q : \Type} (f : Either P Q -> \Sigma P Q) (x : Q) : P => (f (inr x)).1
  }

-- t6 : ((((P -> Q) -> P) -> P) -> Q) -> Q

\func t6 {P Q : \Type} (f : (((P -> Q) -> P) -> P) -> Q) : Q => f (\lam g => g (\lam h => f (\lam _ => h)))

-- 5. Докажите следующие утверждения.

-- s1 : Если для любого x : Nat верно P(x), то существует x : Nat для которого P(x) верно.
\func s1 (P : Nat -> \Type) (h : \Pi (x : Nat) -> P x) : \Sigma (x : Nat) (P x) => (0, h 0)

-- s2 : Если не существует x : Nat такого, что верно P(x), то P(3) не верно.
\func s2 (P : Nat -> \Type) (h : \Sigma (x : Nat) (P x) -> Empty) : P 3 -> Empty => \lam x => h (3, x)

-- 6. Сформулируйте и докажите следующие утверждения.

-- s3 : Если для всех x : Nat верно, что P(x) влечет Q(x), и существует x : Nat для которого верно P(x), то существует x : Nat для которого верно Q(x).

\func s3 (P : Nat -> \Type) (Q : Nat -> \Type) (h : \Pi (x : Nat) -> P x -> Q x) (p : \Sigma (x : Nat) (P x)) : \Sigma (x : Nat) (Q x) =>
  (p.1, h p.1 p.2)

-- s4 : Если для всех x : Nat либо не верно P(x), либо не верно Q(x), то P(3) влечет, что Q(3) не верно.

\func s4 (P : Nat -> \Type) (Q : Nat -> \Type) (h : \Pi (x : Nat) -> Either (P x -> Empty) (Q x -> Empty)) : P 3 -> Q 3 -> Empty =>
  \lam p3 q3 => \case h 3 \with {
    | inl p => p p3
    | inr q => q q3
  }

-- 7. Чему равно fileTest (в нормальной форме)? Докажите это.

\func fileTest' => (17, 8, 13, 55, 18, 21)
\func proofFileTest : fileTest = fileTest' => idp

-- 8. Чему равно M5.t''' (в нормальной форме)? Докажите это.

\func M5' => ((82, 42, 61, 82, 34, 42, 61, 29), 25, (42, 29))
\func proofM5 : M5' = M5.t''' => idp

-- 9. Докажите ассоциативность ++.

\func ++-assoc {A : \Type} (xs ys zs : List A) : (xs ++ ys) ++ zs = xs ++ (ys ++ zs) \elim xs
  | nil => idp
  | cons x xs => pmap (cons x) (++-assoc xs ys zs)