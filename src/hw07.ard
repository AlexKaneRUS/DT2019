\import Utils (idp, pmap, sym, transport)
\import hw02 (gcd)
\import hw06
\import lect05 (funExt)
\import lect07

-- 1. Определите функцию swap несколькими способами.

-- При помощи \cowith и доступа к полям.
\func swap {A B : \Type} (p : Pair A B) : Pair B A \cowith
  | fst_ => p.snd_
  | snd_ => p.fst_

-- При помощи \new и паттерн матчинга.
\func swap' {A B : \Type} (p : Pair A B) : Pair B A
  | (a, b) => \new Pair {
                | fst_ => b
                | snd_ => a
              }


-- При помощи \new и доступа к полям.
\func swap'' {A B : \Type} (p : Pair A B) : Pair B A
  => \new Pair {
    | fst_ => p.snd_
    | snd_ => p.fst_
  }

-- 2. Докажите, что swap (swap p) = p.

\func etaPair {A B : \Type} (p : Pair A B) : p =
  \new Pair A B {
    | fst_ => p.fst_
    | snd_ => p.snd_
  } => idp

\func swap-involutive {A B : \Type} (p : Pair A B) : swap (swap p) = p
  => idp

-- 3. Докажите, что тип PosNat 0 пустой, а тип PosNat 1 нет.

\data Empty

\func zero-isNotPos (p : PosNat 0) : Empty => \case p.p \with {}

\func one-isPos : PosNat 1 \cowith
  | p => tt


-- 4. Определите \record, состоящий из пар взаимно простых натуральных чисел.
--    Определите тип, состоящий из натуральных чисел, взаимно простых с 60.

\record MutPrime (n1 : Nat) (n2 : Nat) (p : gcd n1 n2 = 1)

\data MutPrime60 (MutPrime 60)

-- 5. Определите класс монад, который наследует класс функторов. Определите \instance этого класса для Maybe.

\data Maybe (A : \Type) | nothing | just A

\class Monad \extends Functor {
  | \infix 4 >>= {A B : \Type} : F A -> (A -> F B) -> F B
  | return {A : \Type} : A -> F A
  | bind-assoc {A B C : \Type} (x : F A) (f : A -> F B) (g : B -> F C) : (x >>= f) >>= g = x >>= (\lam x => f x >>= g)
  | id-right {A : \Type} (x : F A) : x >>= return = x
  | id-left {A B : \Type} (x : A) (f : A -> F B) : return x >>= f = f x
  | fmap => \lam f x => x >>= (\lam x => return (f x))
  | fmap-id => id-right
  | fmap-comp =>
    \lam {A B C : \Type} (f : A -> B) (g : B -> C) (y : F A)
      => sym (transport (\lam t => t = y >>= (\lam x => return (g (f x))))
                        (sym (bind-assoc y (\lam x => return (f x)) (\lam x => return (g x))))
                        (helper {A} {B} {C} {F} {return} {>>=} {id-left} y f g))
}

\func helper {A B C : \Type} {F : \Type -> \Type}
             {return : \Pi {A : \Type} -> A -> F A} {bind : \Pi {A B : \Type} -> F A -> (A -> F B) -> F B} {id-left : \Pi {A B : \Type} -> \Pi (x : A) (f : A -> F B) -> return x `bind` f = f x}
             (y : F A) (f : A -> B) (g : B -> C) :
  (y `bind` (\lam (x : A) => return (f x) `bind` (\lam (x : B) => return (g x)))) = (y `bind` (\lam (x : A) => return (g (f x))))
  => pmap (\lam x => y `bind` x) (funExt {A} (\lam _ => F C) (\lam z => id-left (f z) (\lam t => return (g t))))

\instance MaybeMonad : Monad Maybe
  | >>= => maybeBind
  | return => just
  | bind-assoc => maybe-bind-assoc
  | id-right => maybe-id-right
  | id-left => maybe-id-left
  \where {
    \func maybeBind {A B : \Type} (l : Maybe A) (f : A -> Maybe B) : Maybe B \elim l
      | nothing => nothing
      | just x  => f x

    \func maybe-bind-assoc {A B C : \Type} (x : Maybe A) (f : A -> Maybe B) (g : B -> Maybe C)
      : (x `maybeBind` f) `maybeBind` g = x `maybeBind` (\lam x => f x `maybeBind` g) \elim x
        | nothing => idp
        | just x => idp

    \func maybe-id-right {A : \Type} (x : Maybe A) : x `maybeBind` just = x \elim x
      | nothing => idp
      | just x => idp

    \func maybe-id-left {A B : \Type} (x : A) (f : A -> Maybe B) : just x `maybeBind` f = f x => \case (f x) \with {
      | nothing => idp
      | just a => idp
    }
  }

-- 6. Определите инстансы монады для State и State'.

\record State (S A : \Type)
  | state : S -> \Sigma S A

\instance StateMonad (S : \Type) : Monad (\lam A => State S A)
  | >>= => stateBind
  | return => \lam {A : \Type} (x : A) => \new State S A { | state => \lam s => (s, x)}
  | bind-assoc => \lam x f g => idp
  | id-right => \lam x => idp
  | id-left => \lam x f => idp
  \where {
    \func stateBind {S A B : \Type} (l : State S A) (f : A -> State S B) : State S B
    \cowith
      | state => (\lam s => state {f (l.state s).2} (l.state s).1)
  }

\data State' (S A : \Type)
  | state' (S -> \Sigma S A)

\func vToS {S A : \Type} (x : State' S A) : State S A \elim x
  | state' f => \new State S A {
    | state => f
  }

\func fToS {S A B : \Type} (f : A -> State' S B) (x : A) : State S B => vToS (f x)

\instance StateMonad' (S : \Type) {m : Monad (\lam A => State S A)} : Monad (State' S)
  | >>= => \lam {A : \Type} (x : State' S A) f => state' (state {vToS x m.>>= fToS f})
  | return => \lam x => state' (state {m.return x})
  | bind-assoc => \lam {A B C : \Type} x f g
    => pmap state' (pmap (\lam (x : State S C) => state {x}) (m.bind-assoc (vToS x) (fToS f) (fToS g)))
  | id-right => \lam {A : \Type} (x : State' S A) => transport (\lam y => y = x)
                                                               (sym (pmap state' (pmap (\lam (x : State S A) => state {x}) (m.id-right (vToS x)))))
                                                               (helper {A} {S} {m} x)
  | id-left => \lam {A B : \Type} (x : A) (f : A -> State' S B) =>
      transport
      (\lam y => y = f x)
      (sym (pmap state' (pmap (\lam (x : State S B) => state {x}) (m.id-left x (fToS f)))))
      (helper1 {A} {B} {S} {m} x f)
  \where {
    \func helper {A S : \Type} {m : Monad (\lam A => State S A)} (x : State' S A) : state' {S} {A} (state {vToS {S} {A} x}) = x \elim x
      | state' f => idp

    \func helper1 {A B S : \Type} {m : Monad (\lam A => State S A)} (x : A) (f : A -> State' S B) :
      state' (state {vToS (f x)}) = f x => \case f x \with {
        | state' f' => helper2 {A} {B} {S} {m} (f x)
    }

    \func helper2 {A B S : \Type} {m : Monad (\lam A => State S A)} (fx : State' S B) :
      state' (state {vToS fx}) = fx \elim fx
        | state' f' => idp
  }
